var_t = ""
var = ""
EPS = 0.000001
rz = ";"
control_sum = "-"
n = 0

if strlen(gid)<1 then gid = GLOB_INTGUID
ignore_id = "!"
success = LIBRARYGLOBAL ("Libraryflobals-km", "ignore_id", ignore_id)
if abs(success)<EPS then ignore_id = "!"
spec_ignore = 0

IF STRSTR (pos, ignore_id, 1) > 0 THEN spec_ignore = 1
IF STRSTR (subpos, ignore_id, 1) > 0 THEN spec_ignore = 1
show_marker = 1 : show_subpos = 1 : call "Показ_по_сборкам" PARAMETERS ALL subpos=subpos, pos=pos RETURNED_PARAMETERS show_subpos, show_marker
if show_subpos = 0 and show_marker = 0 THEN spec_ignore = 1

version = 3

if not(spec_ignore) then
!-------------------------------------------------
!------------ Материал из архикада ---------------
	if tip_el = "35" and mat_inx>EPS then
		dim desk_arr[3][4]
		for ii=1 to 3
			for jj=1 to 4
				desk_arr[ii][jj] = ""
			next jj
		next ii
	
		!Имя параметра Значение Единица_измерения_1 Единица_измерения_2
		N_ROW_EDIZM = 1
		N_ROW_COST = 2
		N_ROW_ZAP = 3
		N_ROW_TH = 4

		! -------------- Наименование и обозначение берём из штриховки материала -------------------
		naen = "" : f_type = 0
		bSucceed = request{2} ("Building_Material_info", mat_inx, "gs_bmat_cutfill_properties", f_type, en, en)
		n = REQUEST ("Name_of_fill", f_type, naen)
		dim type_obozn[]: i=0
		i=i+1 : type_obozn[i]="(ГОСТ"
		i=i+1 : type_obozn[i]="(ТУ"
		i=i+1 : type_obozn[i]="(Серия"
		i=i+1 : type_obozn[i]="(СТО"
		p_start = 0
		for i=1 to vardim1(type_obozn)
			p_start=STRSTR(naen, type_obozn[i], 1)
			if p_start>EPS then i=vardim1(type_obozn)
		next i
		p_end = 0
		if p_start > EPS then
			t_naen = STRSUB (naen, p_start, strlen(naen))
			p_end = STRSTR(t_naen, ")", 1)
		endif
		if p_start > EPS and p_end > EPS then
			obozn = STRSUB (t_naen, 2, p_end-2)
			naen = STRSUB (naen, 1, p_start-1) + STRSUB (t_naen, p_end+1, strlen(t_naen))
			mts_out_str = ""
			call "macro_trim_space" parameters mts_in_str=naen, RETURNED_PARAMETERS mts_out_str
			naen = mts_out_str
		endif
		!--------------------------------------------------------------------------------------------
		
		! -------------- Из поля Производитель возьмём данные о расходе -------------------
		!Возможные параметры - "Ед.Изм", "Расход", "Кзап"
		desk = ""
		n = REQUEST{2} ("Building_Material_info", mat_inx, "gs_bmat_manufacturer", desk)
		n_t = STRSTR(desk, ";", 1)
		n_par = STRSTR(desk, "=")
		if n_t>0 or n_par>0 then
			for ll = 1 to 3
				n_t = STRSTR(desk, ";", 1)
				if n_t>0 then
					part = STRSUB(desk, 1, n_t-1)
					desk = STRSUB(desk, n_t+1, strlen(desk))
				else
					ll=3
					part = desk
				endif
				n_par = STRSTR(part, "=", 1)
				par = "" : val = "" : cost_1 = "" : cost_2 = "" : n_row = 0
				if n_par>0 then
					par = STRSUB(part, 1, n_par-1)
					if STRSTR(par, "Ед.Изм", 1)>0 then n_row = N_ROW_EDIZM
					if STRSTR(par, "Расход", 1)>0 then n_row = N_ROW_COST
					if STRSTR(par, "Кзап", 1)>0 then n_row = N_ROW_ZAP
					if n_row > 0 then
						val = STRSUB(part, n_par+1, strlen(part))
						n_cost = STRSTR(val, "/", 1) !Если нашли данные о расходе - разобъём единицы измерения
						if n_cost>0 then
							n_edizm = 0
							for nn=1 to n_cost-1
								is_text=1
								symb = STRSUB (val, nn, 1)
								for kk=0 to 9
									if symb=str("%.0", kk) then
										is_text=0
										kk=9
									endif
								next kk
								if is_text=1 then
									if symb="." then is_text=0
									if symb="," then is_text=0
								endif
								if is_text then 
									n_edizm = nn-1
									nn=n_cost-1
								endif
							next nn
							cost_1 = STRSUB(val, n_edizm+1, n_cost-n_edizm-1)
							cost_2 = STRSUB(val, n_cost+1, strlen(val))
							val = STRSUB(val, 1, n_edizm)
						endif
						desk_arr[n_row][1]=par
						desk_arr[n_row][2]=val
						desk_arr[n_row][3]=cost_1
						desk_arr[n_row][4]=cost_2
					endif
				endif
			next ll
		endif

		!Попробуем вычислить недостающие значения площади, объёма и толщины
		if mat_area>EPS and mat_volume>EPS and mat_thickness<EPS then mat_thickness = mat_volume / mat_area
		if mat_area>EPS and mat_thickness>EPS and mat_volume<EPS then mat_volume = mat_area * mat_thickness
		if mat_volume>EPS and mat_thickness>EPS and mat_area<EPS then mat_area = mat_volume / mat_thickness
		
		!По умолчанию - выводим объём в кубах
		qty = mat_volume
		cost = "куб.м."
		
		!Если единицы измерения в спецификации или расходе "кв.м." то берём площадь
		if desk_arr[N_ROW_EDIZM][2]="кв.м." or desk_arr[N_ROW_COST][4]="кв.м." then 
			cost = desk_arr[N_ROW_EDIZM][2]
			qty = mat_area
			if mat_thickness > 0.001 then naen = naen + ", t="+ str("%.0mm",mat_thickness) +"мм."
		endif
		
		!Если нужно выдать массу, но расход не указан - умножаем объём на плотность
		if desk_arr[N_ROW_EDIZM][2]="кг." and desk_arr[N_ROW_COST][1]="" then 
			cost = desk_arr[N_ROW_EDIZM][2]
			density = 0
			n = REQUEST{2} ("Building_Material_info", mat_inx, "gs_bmat_physical_properties", thermalConductivity, density, heatCapacity, embodiedEnergy, embodiedCarbon)
			qty = mat_volume * density
		endif
		
		!Если расход указан - умножаем количество на расход
		if desk_arr[N_ROW_COST][1]<>"" then
			var = desk_arr[N_ROW_COST][2]
			gosub "str2num"
			if vartype(num)=1 and num>EPS then
				qty = qty * num
				cost = desk_arr[N_ROW_COST][3]
			endif
		endif
		
		!Если дана только площадь - выводим в кв.м.
		if mat_area>EPS and mat_volume<EPS and mat_thickness<EPS and qty<EPS then 
			qty = mat_area
			cost = "кв.м."
		endif
		
		!Если дан только объём - выводим в куб.м.
		if mat_area<EPS and mat_volume>EPS and mat_thickness<EPS and qty<EPS then 
			qty = mat_volume
			cost = "куб.м."
		endif
		
		!Добавляем запас
!		if desk_arr[N_ROW_ZAP][1]<>"" then
!			var = desk_arr[N_ROW_ZAP][2]
!			gosub "str2num"
!			if vartype(num)=1 and num>EPS then
!				if num<1.0 then num = 1
!				if num>1.5 then num = 1.5
!				qty = qty * num
!			endif
!		endif
		if qty>EPS then
			tip_el = "30"
			kol_vo_spec = qty
			komment = cost
		endif
	endif
!-------------------------------------------------
	if version = 2 then
		n = REQUEST ("STORY_INFO", "", nelev, t)
		FOR i = 1 TO nelev
			numb_elev_ar[i] = t [4 * (i - 1) + 1]
			name_elev_ar[i] = t [4 * (i - 1) + 2]
			size_elev_ar[i][1] = t [4 * (i - 1) + 3]
			size_elev_ar[i][2] = size_elev_ar[i][1] + t [4 * (i - 1) + 4]
		NEXT i
		inx_elev = -1
		if mun_elev and abs(n_elev)<=abs(nelev) then
			FOR i = 1 TO nelev
				if numb_elev_ar[i]=n_elev then inx_elev = i
			next i
		endif
		if inx_elev = -1 then
			FOR i = 1 TO nelev
				if otm_elem >= size_elev_ar[i][1] and otm_elem<size_elev_ar[i][2] then inx_elev = i
			next i
		endif
		if inx_elev = -1 then
			if otm_elem >= size_elev_ar[nelev][1] or otm_elem>=size_elev_ar[nelev][2] then inx_elev = nelev
		endif
		if inx_elev = -1 then
			if otm_elem <= size_elev_ar[1][1] or otm_elem<=size_elev_ar[1][2] then inx_elev = 1
		endif
		if inx_elev = -1 then
			n_elev = 99
			elem_elev="ОЭ"
		else
			n_elev = numb_elev_ar[inx_elev]
			elem_elev = name_elev_ar[inx_elev]
		endif
		!Если имя этажа слишком длинной - оставим только хвост
		characters_number = 10
		if STRLEN(elem_elev)>characters_number then
			elem_elev = STRSUB (elem_elev, STRLEN(elem_elev)-characters_number, characters_number+1)
		endif
		!Переводим все числовые значения в текст, обрезаем нулив конце
		n_elev_txt = str("%.0",n_elev)
	endif
	if version >= 3 then
		elem_elev="-"
		n_elev_txt = "-"
	endif

	if abs(otm_elem) < EPS then 
		otm_elem_t = "0"
	else
		var = STR("%.3m", otm_elem) : gosub "del_zero" : otm_elem_t = var_t
	endif
	if ves < EPS then 
		ves_t = "-"
	else
		var = str("%.3",ves) : gosub "del_zero" : ves_t = var_t
	endif
	if kol_vo_spec<EPS then
		kol_vo_spec = 1
		kol_vo_txt = "1"
	else
		var = str("%.3",kol_vo_spec) : gosub "del_zero" : kol_vo_txt = var_t
	endif
	if vartype(subpos) = 1 then subpos = str("%.0",subpos)
	if vartype(pos) = 1 then pos = str("%.0",pos)
	if vartype(marka) = 1 then marka = "!"
	if vartype(obozn) = 1 then obozn = "!"
	if vartype(naen) = 1 then naen = "!"
	if vartype(komment) = 1 then komment = "!"
	if vartype(spec_add_param) = 1 then spec_add_param = "-"
	if subpos = "" then subpos = "-"
	if subpos = " " then subpos = "-"
	if subpos = "!" then subpos = "-"
	if pos="" then pos = "-"
	if marka="" then marka = "-"
	if obozn="" then obozn = "-"
	if naen="" then naen = "-"
	if komment="" then komment = "-"
	spec_add_param = "v"+STR("%.0", version)+"%%" + spec_add_param


!-------------------------------------------------
!-------------- Вывод в ведомость ----------------


if use_spec = 0 then
	drawing
	database_set  "_ArchiBibl_BD"
	if ves < EPS then 
		ves_t = "-"
	else
		var = str("%.2",ves) : gosub "del_zero" : ves_t = var_t
	endif

	if tip_el = "60" then
		component naen,kol_vo_spec,subpos,0, ves_t,obozn
		end
	endif

	if tip_el = "70" then end

	if tip_el = "71" then
		component naen,kol_vo_spec,pos,0, ves_t,obozn
		end
	endif
endif



!-------------------------------------------------
	max_naen = 60
	if strlen(naen)>max_naen and tip_el <> "60" then naen = strsub(naen,1,max_naen)

!-------------------------------------------------
!------------------- Арматура --------------------
	if tip_el = "10" then
		r_arm = diam_arm/2
		if diam_arm<EPS then 
			diam_arm_txt = "!"
		else
			diam_arm_txt = str("%.0mm",diam_arm)
		endif
		if dlin_st<EPS then
			dlin_txt = "!"
		else
			dlin_txt = str("%.0mm", dlin_st)
		endif
		if kol_vo_spec<EPS then
			kol_vo_txt = "1"
		else
			kol_vo_txt = str("%.0",kol_vo_spec)
		endif
		str_out = subpos + rz !Сборочная позиция
		str_out = str_out + tip_el + rz !Тип элемента
		str_out = str_out + pos + rz !Позиция
		str_out = str_out + n_elev_txt + rz !Номер этажа
		str_out = str_out + elem_elev + rz !Этаж
		str_out = str_out + spec_add_param + rz !Доп. параметры
		str_out = str_out + marka + rz !Класс
		str_out = str_out + diam_arm_txt + rz !Диаметр
		str_out = str_out + dlin_txt + rz !Длина
		str_out = str_out + kol_vo_txt + rz !Количество
		fon_txt = str("%.0",fon) : str_out = str_out + fon_txt + rz !Погонаж ли
		plit_txt = str("%.0",plit) : str_out = str_out + plit_txt + rz !Фон ли плиты
		gnut_txt = str("%.0",gnut) : str_out = str_out + gnut_txt + rz !Гнутый ли стержень
		str_out = str_out + control_sum !Контрольная сумма
		COMPONENT str_out, dlin_st * r_arm * r_arm * PI * 1000, gid
		if gnut then
			tip_el = "15"
			str_out = subpos + rz !Сборочная позиция
			str_out = str_out + tip_el + rz !Тип элемента
			str_out = str_out + pos + rz !Позиция
			str_out = str_out + n_elev_txt + rz !Номер этажа
			str_out = str_out + elem_elev + rz !Этаж
			str_out = str_out + spec_add_param + rz !Доп. параметры
			for j=1 to vardim1(arm_coord)
				var = arm_coord[j]
				if vartype(var)=1 then var_t = str("%.0mm", var)
				str_out = str_out + var_t + "/"
			next j
			COMPONENT str_out, dlin_st * r_arm * r_arm * PI * 1000, gid
		endif
	else
		n = n + 1
	endif
!-------------------------------------------------

!-------------------------------------------------
!--------------------- Прокат --------------------
	if tip_el = "20" then
		if vartype(tip_konstr) = 1 then tip_konstr = "!"
		if vartype(tip_profile) = 1 then tip_profile = "!"
		if vartype(mater) = 1 then mater = "!"
		if dlin_prof>EPS and STRSTR(tip_profile, "--")=0 and STRSTR(naen, "--")=0 then
			dlin_prof = round_int(dlin_prof*1000)/1000
		endif
		dlin_prof_txt = ""
		if dlin_prof<EPS then 
			dlin_prof_txt = "-"
		else
			if dlin_prof>0.001 then
				dlin_prof_txt = str("%.0mm",dlin_prof)
			else
				dlin_prof_txt = str("%.3mm",dlin_prof)
			endif
		endif
		characters_number = 20
		if STRLEN(naen)>characters_number then naen = STRSUB (naen, 1, characters_number)
		str_out = subpos + rz !Сборочная позиция  
		str_out = str_out + tip_el + rz !Тип элемента
		str_out = str_out + pos + rz !Позиция
		str_out = str_out + n_elev_txt + rz !Номер этажа
		str_out = str_out + elem_elev + rz !Этаж
		str_out = str_out + spec_add_param + rz !Доп. параметры
		str_out = str_out + tip_konstr + rz !Тип конструкции
		str_out = str_out + "-" + rz !ГОСТ на сталь
		str_out = str_out + mater + rz !Сталь
		str_out = str_out + kol_vo_txt + rz !Количество (=1 для обычных библ. элементов)
		str_out = str_out + obozn + rz !ГОСТ на профиль
		str_out = str_out + tip_profile + rz !Профиль
		str_out = str_out + dlin_prof_txt + rz !Длина
		str_out = str_out + control_sum + rz !Контрольная сумма
		str_out = str_out + ves_t+ rz !Вес погонный
		str_out = str_out + naen !Наименование
		COMPONENT str_out, ves, gid
	else
		n = n + 1
	endif
!-------------------------------------------------

!-------------------------------------------------
!-------------------- Изделия --------------------
	if tip_el = "40" then
		str_out = subpos + rz !Сборочная позиция  
		str_out = str_out + tip_el + rz !Тип элемента
		str_out = str_out + pos + rz !Позиция
		str_out = str_out + n_elev_txt + rz !Номер этажа
		str_out = str_out + elem_elev + rz !Этаж
		str_out = str_out + spec_add_param + rz !Доп. параметры
		str_out = str_out + obozn + rz !Обозначение
		str_out = str_out + naen + rz !Наименование
		str_out = str_out + ves_t + rz !Вес
		str_out = str_out + kol_vo_txt + rz !
		str_out = str_out + komment !
		COMPONENT str_out, kol_vo_spec, gid
	else
		n = n + 1
	endif
!-------------------------------------------------

!-------------------------------------------------
!------------------- Материал --------------------
	if tip_el = "30" then
		if strlen(tip_konstr)>0 and tip_konstr<>" " then spec_add_param = spec_add_param + "@tk="+tip_konstr
		str_out = subpos + rz !Сборочная позиция  
		str_out = str_out + tip_el + rz !Тип элемента
		str_out = str_out + " " + rz !Позиция
		str_out = str_out + n_elev_txt + rz !Номер этажа
		str_out = str_out + elem_elev + rz !Этаж
		str_out = str_out + spec_add_param + rz !Доп. параметры
		str_out = str_out + obozn + rz !Обозначение
		str_out = str_out + naen + rz !Наименование
		str_out = str_out + "-" + rz !Вес
		str_out = str_out + kol_vo_txt + rz !Обозначение
		str_out = str_out + komment !Наименование
		COMPONENT str_out, kol_vo_spec, gid
	else
		n = n + 1
	endif
!-------------------------------------------------

!-------------------------------------------------
!------------ Маркировка сборки ------------------
	if tip_el = "45" then
		str_out = subpos + rz !Сборочная позиция  
		str_out = str_out + tip_el + rz !Тип элемента
		str_out = str_out + subpos + rz !Позиция
		str_out = str_out + n_elev_txt + rz !Номер этажа
		str_out = str_out + elem_elev + rz !Этаж
		str_out = str_out + spec_add_param + rz !Доп. параметры
		str_out = str_out + obozn + rz !Обозначение
		str_out = str_out + naen + rz !Наименование
		str_out = str_out + "-" + rz !Вес
		str_out = str_out + kol_vo_txt + rz !
		str_out = str_out + komment !
		COMPONENT str_out, kol_vo_spec, gid
	else
		n = n + 1
	endif
!-------------------------------------------------

!-------------------------------------------------
!----------- Заполнение проёмов ------------------
	if tip_el = "60" then
		str_out = subpos + rz !Сборочная позиция
		str_out = str_out + tip_el + rz !Тип элемента
		str_out = str_out + pos + rz !Позиция
		str_out = str_out + n_elev_txt + rz !Номер этажа
		str_out = str_out + elem_elev + rz !Этаж
		str_out = str_out + spec_add_param + rz !Доп. параметры
		str_out = str_out + obozn + rz !Обозначение
		str_out = str_out + naen + rz !Наименование
		str_out = str_out + ves_t + rz !Вес
		str_out = str_out + kol_vo_txt + rz !Количество
		str_out = str_out + komment !Комментарий
		if strlen(str_out)>100 then
			str_out = subpos + rz !Сборочная позиция
			str_out = str_out + "61" + rz !Тип элемента
			str_out = str_out + pos + rz !Позиция
			str_out = str_out + n_elev_txt + rz !Номер этажа
			str_out = str_out + elem_elev + rz !Этаж
			str_out = str_out + spec_add_param + rz !Доп. параметры
			str_out = str_out + obozn + rz !Обозначение
			str_out = str_out + "-" + rz !Наименование
			str_out = str_out + ves_t + rz !Вес
			str_out = str_out + kol_vo_txt + rz !Количество
			str_out = str_out + komment !Комментарий
			COMPONENT str_out, 1, gid

			str_out = subpos + rz !Сборочная позиция
			str_out = str_out + "62" + rz !Тип элемента
			str_out = str_out + naen !Наименование
			COMPONENT str_out, 1, gid
		else
			COMPONENT str_out, 1, gid
		endif
	else
		n = n + 1
	endif
!-------------------------------------------------

!-------------------------------------------------
!---- Элементы для пятиграфки смежников ----------
	if tip_el = "80" then
		if eq_code>EPS then
			eq_code = str("%.0",eq_code)
		else
			eq_code = "-"
		endif
		str_out = subpos + rz !Сборочная позиция
		str_out = str_out + tip_el + rz !Тип элемента
		str_out = str_out + pos + rz !Позиция
		str_out = str_out + n_elev_txt + rz !Номер этажа
		str_out = str_out + elem_elev + rz !Этаж
		str_out = str_out + spec_add_param + rz !Доп. параметры
		str_out = str_out + obozn + rz !Обозначение
		str_out = str_out + naen + rz !Наименование
		str_out = str_out + ves_t + rz !Вес
		str_out = str_out + kol_vo_txt + rz !Количество
		str_out = str_out + komment !Комментарий
		str_out = str_out + eq_code + rz !Код
		str_out = str_out + eq_plant !Комментарий
		COMPONENT str_out, 1, gid
	else
		n = n + 1
	endif
!-------------------------------------------------

!-------------------------------------------------
!------------- Элементы перемычки ----------------
	if tip_el = "71" then
		spec_add_param = spec_add_param + "@Z="+otm_elem_t
		str_out = subpos + rz !Сборочная позиция  
		str_out = str_out + tip_el + rz !Тип элемента
		str_out = str_out + pos + rz !Позиция
		str_out = str_out + n_elev_txt + rz !Номер этажа
		str_out = str_out + elem_elev + rz !Этаж
		str_out = str_out + spec_add_param + rz !Доп. параметры
		str_out = str_out + obozn + rz !Обозначение
		str_out = str_out + naen + rz !Наименование
		str_out = str_out + ves_t + rz !Вес
		str_out = str_out + kol_vo_txt + rz !
		str_out = str_out + komment !
		COMPONENT str_out, kol_vo_spec, gid
	else
		n = n + 1
	endif
!-------------------------------------------------

!-------------------------------------------------
!------------ Маркировка перемычки ---------------
	if tip_el = "70" then
		spec_add_param = spec_add_param + "@Z="+otm_elem_t
		str_out = subpos + rz !Сборочная позиция  
		str_out = str_out + tip_el + rz !Тип элемента
		str_out = str_out + subpos + rz !Позиция
		str_out = str_out + n_elev_txt + rz !Номер этажа
		str_out = str_out + elem_elev + rz !Этаж
		str_out = str_out + spec_add_param + rz !Доп. параметры
		str_out = str_out + obozn + rz !Обозначение
		str_out = str_out + naen + rz !Наименование
		str_out = str_out + "-" + rz !Вес
		str_out = str_out + kol_vo_txt + rz !
		str_out = str_out + komment !
		COMPONENT str_out, kol_vo_spec, gid
	else
		n = n + 1
	endif
!-------------------------------------------------
endif
var = ""
end

"str2num":
	!---------- String to num ---------------
	num = 0 : dot = "."
	if vartype(var)=2 then
		if strstr(var, dot)<EPS then dot = ","
		n_t = strstr(var, dot)
		if n_t > 0 then
			t_int = STRSUB(var, 1, n_t-1)
			t_ceil = STRSUB(var, n_t+1, strlen(var))
			x = SPLIT (t_int, "%n", n_int)
			x = SPLIT (t_ceil, "%n", n_ceil)
			n_zero = 10**strlen(t_ceil)
			num = n_int + n_ceil/n_zero
			!		re_var = str("%."+str("%.0",strlen(t_ceil)), num)
			!		check = (re_var = var)
			!Доработать проверку для разных разделителей
		else
			x = SPLIT (var, "%n", num)
		endif
	else
		x = var
	endif
	!---------------------------------------
return

"del_zero":
	var_t = var
!	var_t = ""
!	if STRSTR(var, ".")> 0 or STRSTR(var, ",")> 0 then
!		for i=strlen(var) to 1 step -1
!			z = strsub(var,i,1)
!			if z<>"0" or z="." or z="," then 
!				var_t = strsub(var,1,i-1*(i=strlen(var)-1)-1*(i=strlen(var)))
!			endif
!			if var_t <> "" then i = 1
!		next i
!		fval = 1 : eval = 2
!		n1 = SPLIT (var_t, "%n", fval)
!		n2 = SPLIT (var, "%n", eval)
!		if n1<>1 or n2<>1 or fval<>eval then var_t = var !Фокус не удался
!	else
!		var_t = var
!	endif
return



