if haskey(poly_data.is2d) then
	if not(poly_data.is2d) then end
endif

if abs((rx mod 360)-180)<EPS then mul2 1,-1
if abs((ry mod 360)-180)<EPS then mul2 -1,1
if abs(rx mod 360)<EPS and abs(ry mod 360)<EPS then rot2 rz

uniID=6500 
salta_punti_editabili = 0 
FOR x = 1 TO poly_n -1+poly_closed
	IF ABS(ABS(poly_pointbis_x [x])- ABS(poly_pointbis_check_x [x]))> 0.001 THEN 
		salta_punti_editabili = 1
	ELSE
		IF ABS(ABS(poly_pointbis_y [x])- ABS(poly_pointbis_check_y [x])) > 0.001 THEN 
			salta_punti_editabili = 1
		ENDIF
	ENDIF 
NEXT x   
r = 0.1
IF salta_punti_editabili = 0 THEN
	FOR x = 1 TO poly_n-1+poly_closed
		if use_x then
			desc = "Новая вершина " + str("%.0", x) +" x"
			HOTSPOT2 0,poly_pointbis_check_y [x],uniID,poly_pointbis_x [x],1+128,poly_pointbis_x [x],desc : uniID=uniID+1
			HOTSPOT2 poly_pointbis_check_x [x],poly_pointbis_check_y [x],uniID,poly_pointbis_x [x],2,poly_pointbis_x [x],desc : uniID=uniID+1
			HOTSPOT2 -1,poly_pointbis_check_y [x],uniID,poly_pointbis_x [x],3,poly_pointbis_x [x],desc : uniID=uniID+1
		ENDIF
		if use_y then
			desc = "Новая вершина " + str("%.0", x) +" y"
			HOTSPOT2 poly_pointbis_check_x [x],0,uniID,poly_pointbis_y [x],1+128,poly_pointbis_y [x],desc : uniID=uniID+1
			HOTSPOT2 poly_pointbis_check_x [x],poly_pointbis_check_y [x],uniID,poly_pointbis_y [x],2,poly_pointbis_y [x],desc : uniID=uniID+1
			HOTSPOT2 poly_pointbis_check_x [x],-1,uniID,poly_pointbis_y [x],3,poly_pointbis_y [x],desc : uniID=uniID+1
		ENDIF
		if edit_mode THEN circle2 poly_pointbis_check_x [x],poly_pointbis_check_y [x], r
	NEXT X
ENDIF
FOR x = 1 TO poly_n
	if use_x then
		desc = "Вершина " + str("%.0", x) +" x"
		HOTSPOT2 0,poly_point_xy [x][2],uniID,poly_point_xy [x][1],1+128,poly_point_xy [x][1],desc  : uniID=uniID+1
		HOTSPOT2 poly_point_xy [x][1],poly_point_xy [x][2],uniID,poly_point_xy [x][1],2,poly_point_xy [x][1],desc : uniID=uniID+1
		HOTSPOT2 -1,poly_point_xy [x][2],uniID,poly_point_xy [x][1],3,poly_point_xy [x][1],desc : uniID=uniID+1
	ENDIF
	if use_y then
		desc = "Вершина " + str("%.0", x) +" y"
		HOTSPOT2 poly_point_xy [x][1],0,uniID,poly_point_xy [x][2],1+128,poly_point_xy [x][2],desc : uniID=uniID+1
		HOTSPOT2 poly_point_xy [x][1],poly_point_xy [x][2],uniID,poly_point_xy [x][2],2,poly_point_xy [x][2],desc : uniID=uniID+1
		HOTSPOT2 poly_point_xy [x][1],-1,uniID,poly_point_xy [x][2],3,poly_point_xy [x][2],desc : uniID=uniID+1
	ENDIF
	if edit_mode THEN circle2 poly_point_xy [x][1],poly_point_xy [x][2], r
	IF (ABS(poly_point_xy [x][1]-poly_point_xy [x+1][1]) > EPS) OR (ABS(poly_point_xy [x][2]-poly_point_xy [x+1][2]) > EPS) THEN
		IF x < poly_n+poly_closed THEN
			IF poly_point_xy [x][1] > poly_point_xy [x+1][1] THEN corr_x1 = 1 ELSE corr_x1 = 0
			xx1 = (poly_point_xy [x+1][1]- poly_point_xy [x][1])
			yy1 = (poly_point_xy [x+1][2]- poly_point_xy [x][2])
			IF ABS(xx1) > EPS THEN
				a1= ATN(yy1/xx1)+corr_x1*180 
			ELSE
				a1 = 90*SGN(yy1)
			ENDIF
			IF a1 < EPS AND a1 > -EPS THEN 
				d1 = ABS(xx1)		
			ELSE 
				d1 = ABS(yy1/sin(a1))
			ENDIF
			ADD2 xx1/2+poly_point_xy [x][1],yy1/2+poly_point_xy [x][2]
			ROT2 a1-90
			IF salta_punti_editabili = 0 and use_d THEN
				HOTSPOT2 0,0,uniID,poly_3point_d [x],1+128 : uniID=uniID+1
				HOTSPOT2 poly_3point_d [x]-.02,0,uniID,poly_3point_d [x],2 : uniID=uniID+1
				HOTSPOT2 -1,0,uniID,poly_3point_d [x],3 : uniID=uniID+1
			ENDIF
			DEL 2
			if x>1 then
			ADD2 poly_point_xy [x][1],poly_point_xy [x][2]
			rot2 a1/2
			add2 -0.01,0
			IF salta_punti_editabili = 0 and use_arc THEN
				HOTSPOT2 0,0,uniID,poly_arc_r[x],1+128 : uniID=uniID+1
				HOTSPOT2 -poly_arc_r[x],0,uniID,poly_arc_r[x],2 : uniID=uniID+1
				HOTSPOT2 1,0,uniID,poly_arc_r[x],3 : uniID=uniID+1
			ENDIF
			DEL 3
			endif
		ENDIF
	ENDIF
NEXT x

if show_line then
	FOR x = 1 TO poly_n-1+poly_closed
		IF (ABS(poly_point_xy [x][1]-poly_point_xy [x+1][1]) > EPS) OR (ABS(poly_point_xy [x][2]-poly_point_xy [x+1][2]) > EPS) THEN
			IF poly_3point_d [x] < EPS AND poly_3point_d [x] > -EPS THEN 
				LINE2 poly_point_xy [x][1],poly_point_xy [x][2],poly_point_xy [x+1][1],poly_point_xy [x+1][2]
				hotLINE2 poly_point_xy [x][1],poly_point_xy [x][2],poly_point_xy [x+1][1],poly_point_xy [x+1][2]
			ELSE
				ARC2 poly_3point_xy [x][1],poly_3point_xy [x][2],poly_3point_radius [x],poly_3point_angle [x][1],poly_3point_angle [x][2]
			ENDIF
		ENDIF	
	NEXT x
endif

if abs((rx mod 360)-180)<EPS then del 1
if abs((ry mod 360)-180)<EPS then del 1
