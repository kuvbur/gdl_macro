! Входные параметры
! mrh_ref_story - Индекс уровня привязки
! Выходные параметры
! mrh_reference_heigh - Высота уровня привязки
! mrh_ref_story_text - Массив имён уровней привязки
! mrh_ref_story_val - Массив индексов цровней привязки
! Использование:
!mrh_ref_story = ref_story
!dim mrh_ref_story_text[7] : mrh_ref_story_text[1] = ""
!dim mrh_ref_story_val[7] : mrh_ref_story_val[1] = 0
!mrh_reference_heigh = 0
!call "macro_reference_height" parameters mrh_ref_story = mrh_ref_story,
!								RETURNED_PARAMETERS mrh_reference_heigh, mrh_ref_story_text, mrh_ref_story_val
!values{2} "ref_story" mrh_ref_story_val, mrh_ref_story_text

name1="" : elev1=0
name2="" : elev2=0
name3="" : elev3=0
name4="" : elev4=0
n = REQUEST ("REFERENCE_LEVEL_DATA", "", name1, elev1, name2, elev2, name3, elev3, name4, elev4)

REF_STORY_CURR = 1
REF_STORY_HOME = 2
REF_STORY_NAME1	= 4
REF_STORY_NAME2	= 5
REF_STORY_NAME3	= 6
REF_STORY_NAME4	= 7

dim tRefStory[]
dim iRefStory[] : i=0
i=i+1 : iRefStory[i]=0 : tRefStory[i] = `Нет`
i=i+1 : iRefStory[i]=REF_STORY_CURR : tRefStory[i] = `Текущий этаж`
i=i+1 : iRefStory[i]=REF_STORY_HOME : tRefStory[i] = `Собств. этаж`
i=i+1 : iRefStory[i]=REF_STORY_NAME1 : tRefStory[i] = name1
i=i+1 : iRefStory[i]=REF_STORY_NAME2 : tRefStory[i] = name2
i=i+1 : iRefStory[i]=REF_STORY_NAME3 : tRefStory[i] = name3
i=i+1 : iRefStory[i]=REF_STORY_NAME4 : tRefStory[i] = name4

_referenceHeightMod	= 0
if GLOB_SCRIPT_TYPE<>5 then
	if mrh_ref_story = REF_STORY_HOME then _referenceHeightMod = GLOB_HSTORY_ELEV
	if mrh_ref_story = REF_STORY_CURR then _referenceHeightMod = -GLOB_CSTORY_ELEV
endif
if mrh_ref_story = REF_STORY_NAME1 then _referenceHeightMod = elev1
if mrh_ref_story = REF_STORY_NAME2 then _referenceHeightMod = elev2
if mrh_ref_story = REF_STORY_NAME3 then _referenceHeightMod	= elev3
if mrh_ref_story = REF_STORY_NAME4 then _referenceHeightMod	= elev4

dim mrh_ref_story_text[7] : mrh_ref_story_text[1] = ""
dim mrh_ref_story_val[7] : mrh_ref_story_val[1] = 0
mrh_reference_heigh = _referenceHeightMod
mrh_ref_story_text = tRefStory
mrh_ref_story_val = iRefStory
!parameters mrh_reference_heigh = mrh_reference_heigh, mrh_ref_story_text=mrh_ref_story_text, mrh_ref_story_val=mrh_ref_story_val
EXIT mrh_reference_heigh, mrh_ref_story_text, mrh_ref_story_val
