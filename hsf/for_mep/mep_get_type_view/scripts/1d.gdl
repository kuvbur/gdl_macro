!! Получение положения МЕР элемента
!! Вызов:
!! call "mep_get_type_view" parameters MEP_Position = MEP_Position, MEP_StraightLength = MEP_StraightLength, RETURNED_PARAMETERS mep_el_type_view, mep_el_sech
EPS = 0.0001

MEP_HORIZONTAL = 1
MEP_VERTICAL = 2 
MEP_INCLINED = 3

el_type_view_ = 0
rx1 = MEP_Position[1][1]
ry1 = MEP_Position[1][2]
rz1 = MEP_Position[1][3]
rx2 = rx1+MEP_Position[2][1]*MEP_StraightLength
ry2 = ry1+MEP_Position[2][2]*MEP_StraightLength
rz2 = rz1+MEP_Position[2][3]*MEP_StraightLength
dx = rx2-rx1
dy = ry2-ry1
dz = rz2-rz1
if abs(dx)<EPS and abs(dy)<EPS and abs(dz)<EPS then
	dx = MEP_Position[3][1]
	dy = MEP_Position[3][2]
	dz = MEP_Position[3][3]
endif
if abs(dx)>EPS or abs(dy)>EPS or abs(dz)>EPS then
	n_ax = (abs(dx)>EPS)+(abs(dy)>EPS)+(abs(dz)>EPS)
	if n_ax=1 and abs(dz)>EPS then el_type_view_ = MEP_VERTICAL * sgn(dz)
	if n_ax=1 and abs(dz)<EPS then el_type_view_ = MEP_HORIZONTAL
	if n_ax=2 then el_type_view_ = MEP_INCLINED * sgn(dz)
endif

type_show = 1
if GLOB_SCRIPT_TYPE = 2 then
	if GLOB_VIEW_TYPE = 2 and GLOB_PREVIEW_MODE = 0 and GLOB_FEEDBACK_MODE = 0  then
		_SYMB_POS_Z = SYMB_POS_Z
		IF el_type_view_ = MEP_HORIZONTAL then
			_MEP_StraightLength = 0
		else
			_MEP_StraightLength = MEP_StraightLength
			IF rz1 > rz2 THEN _SYMB_POS_Z = _SYMB_POS_Z-_MEP_StraightLength
		endif
		call "mep_get_type_cut" parameters all, z_down=_SYMB_POS_Z, z_up=_SYMB_POS_Z+_MEP_StraightLength, AC_AutoOnStoryVisibility=AC_AutoOnStoryVisibility RETURNED_PARAMETERS type_show
	endif
endif
mep_el_sech = type_show
mep_el_type_view = el_type_view_
EXIT mep_el_type_view, mep_el_sech
