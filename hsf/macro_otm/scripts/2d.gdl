if polozh = 0 then polozh = 1
h_polki=0
n_preobr = 0

if polozh = 1 then
	anch_v = 9
	anch_n = 3
endif

if polozh = 2 then
	anch_v = 7
	anch_n = 1
endif

if polozh = 3 then
	anch_v = 9
	anch_n = 3
endif

if polozh = 4 then
	anch_v = 7
	anch_n = 1
	h_polki = 2
endif

PAPER_TO_MODEL = GLOB_SCALE / 1000
DEFINE STYLE{2} "razm" Shrift , Shrift_razm, Shrift_tip
txt_width = 0
otm_max_len = 0
for i=1 to vardim1(otm)
	txtv = otm[i][1] : tblockname = "otmetka_v"+str("%.0",i) : name_style = "razm" : razm_anc = anch_v : gosub "get_block"
	txt_width = max(tbwidth, txt_width)
	txtv = otm[i][2] : tblockname = "otmetka_n"+str("%.0",i) : name_style = "razm" : razm_anc = anch_n : gosub "get_block"
	txt_width = max(tbwidth, txt_width)
	otm_max_len=max(otm_max_len, strlen(otm[i][1]), strlen(otm[i][2]))
next i
txt_width = txt_width + 2*PAPER_TO_MODEL
 
if otm_max_len<EPS then end
if dlin_polki<EPS then dlin_polki = 10*PAPER_TO_MODEL
if h_strelki<EPS then h_strelki = tbheight/2
if vistup_strelki<EPS then vistup_strelki = h_strelki
if dlin_vin<EPS then dlin_vin = txt_width

gosub "mulp" : n_preobr = n_preobr + n_mul
add2 coord[1][1], coord[1][2] : n_preobr = n_preobr + 1
line2 0,0,-dlin_polki,0
line2 -dlin_polki+vistup_strelki,0,-dlin_polki+vistup_strelki-h_strelki,h_strelki
line2 -dlin_polki+vistup_strelki,0,-dlin_polki+vistup_strelki+h_strelki,h_strelki
h_polki=h_strelki

for i=1 to vardim1(otm)
	otmetka_v=otm[i][1]
	otmetka_n=otm[i][2]
	if strlen(otmetka_v)>EPS or strlen(otmetka_n)>EPS then
		n = REQUEST("TEXTBLOCK_INFO", "otmetka_v"+str("%.0",i), width, height)
		height = height * PAPER_TO_MODEL
		h_polki = h_polki + height
		line2 -dlin_polki+vistup_strelki,h_polki,-dlin_polki+vistup_strelki-dlin_vin,h_polki
		line2 -dlin_polki+vistup_strelki,0,-dlin_polki+vistup_strelki,h_polki
		if strlen(otmetka_v)>EPS then
			add2 -dlin_polki+vistup_strelki,h_polki
			gosub "mulp"
			RICHTEXT2 -2*PAPER_TO_MODEL*sgnmulx, -dx_txt,"otmetka_v"+str("%.0",i)
			del n_mul+1
		endif
		if strlen(otmetka_n)>EPS then
			add2 -dlin_polki+vistup_strelki,h_polki
			gosub "mulp"
			RICHTEXT2 -2*PAPER_TO_MODEL*sgnmulx, dx_txt,"otmetka_n"+str("%.0",i)
			del n_mul+1
		endif
	endif
next i

if n_preobr>EPS then del n_preobr
	
end

"mulp":
	sgnmulx = 1 : sgnmuly = 1
	if polozh = 2 then 
		sgnmulx = -1 : sgnmuly = 1
	endif
	if polozh = 3 then
		sgnmulx = 1 : sgnmuly = -1
	endif
	if polozh = 4 then
		sgnmulx = -1 : sgnmuly = -1
	endif
	n_mul = 0
	if (sgnmulx<EPS or sgnmuly<EPS) and abs(sgnmulx)>EPS and abs(sgnmuly)>EPS then
		mul2 sgnmulx, sgnmuly
		n_mul = 1
	endif
return

"get_block":
	width_factor = 1
	charspace_factor = 1
	if vartype(tblockname)<>2 then tblockname = "emp"
	if vartype(txtv)<>2 then txtv = str("%.0",txtv)
	paragraph tblockname+"p" 2,
			0, 0, 0,
			1
			style name_style
			txtv
	endparagraph
	TEXTBLOCK tblockname 0, razm_anc, 0, width_factor, charspace_factor, 1,tblockname+"p"
	width = Shrift_razm : height = Shrift_razm
	n = REQUEST("TEXTBLOCK_INFO", tblockname, width, height)
	tbwidth = width * PAPER_TO_MODEL
	tbheight = height * PAPER_TO_MODEL
	sss=request("Height_of_style", name_style, height, descent, leading)
	dx_txt = (height/2-descent-leading-Shrift_razm/2)*PAPER_TO_MODEL
return
