EPS = 0.0001
space = " " : var = subpos : gosub "trim_space" : subpos = var
space = " " : var = subpos : gosub "trim_space" : subpos = var

show=0
show_mark=0

show_all_subpos = 0
success = LIBRARYGLOBAL ("Libraryflobals-subpos", "show_all_subpos", show_all_subpos)
if abs(success)<EPS then
	show=1
	show_mark=1
	goto "end_show"
endif

if subpos<>"" and subpos<>" " and show_all_subpos then
	show=1
	show_mark=1
	goto "end_show"
endif

show_no_subpos = 0
dim n_id[] : n_id[1]=0
dim show_subpos[] : show_subpos[1] = ""
success = LIBRARYGLOBAL ("Libraryflobals-subpos", "show_no_subpos", show_no_subpos)
success = LIBRARYGLOBAL ("Libraryflobals-subpos", "n_id", n_id)
success = LIBRARYGLOBAL ("Libraryflobals-subpos", "show_subpos", show_subpos)

if (subpos="" or subpos=" ") and n_id[vardim1(show_subpos)]=0 and show_no_subpos then
	show=1
	show_mark=1
	goto "end_show"
endif

show_invert_subpos = 0
dim show_id[] : show_id[1] = ""
dim show_invert_pos[] : show_invert_pos[1] = 0
dim show_no_subposmarker[] : show_no_subposmarker[1] = 0
dim t_show_subpos_id[] : t_show_subpos_id[1] = ""
success = LIBRARYGLOBAL ("Libraryflobals-subpos", "show_id", show_id)
success = LIBRARYGLOBAL ("Libraryflobals-subpos", "show_no_subposmarker", show_no_subposmarker)
success = LIBRARYGLOBAL ("Libraryflobals-subpos", "show_invert_subpos", show_invert_subpos)
success = LIBRARYGLOBAL ("Libraryflobals-subpos", "show_invert_pos", show_invert_pos)
success = LIBRARYGLOBAL ("Libraryflobals-subpos", "show_subpos_id", t_show_subpos_id)

dim show_subpos_id[][] : show_subpos_id[1][1] = ""
q = vardim1(t_show_subpos_id) / vardim1(show_subpos)
k = 0
for i=1 to vardim1(show_subpos)
	for j=1 to q
		k = k + 1
		show_subpos_id[i][j] = t_show_subpos_id[k]
	next j
next i

if (subpos="" or subpos=" ") and show_no_subpos then
	i=vardim1(show_subpos)
	show_id[i]=0
	gosub "check_show_pos"
	if stop_flag_pos>EPS then show = 1
	if show_invert_pos[i] then show = not(show)
	show_mark = show
	goto "end_show"
endif

if subpos<>"" and subpos<>" " then
	if show_invert_subpos then
		show = 1
		show_mark = 1
	endif
	for i=1 to vardim1(show_subpos)-1
		gosub "check_show_subpos"
		if stop_flag_subpos>EPS then
			if show_no_subposmarker[i]=0 then show_mark=1
			gosub "check_show_pos"
			if stop_flag_pos>EPS then
				show = 1
				if stop_flag_subpos=2 and stop_flag_pos=2 and show_invert_pos[i] and not(show_invert_subpos) then show = 0
			endif
			goto 33
		endif
	next i
33:
	if stop_flag_subpos=2 and stop_flag_pos=0 and show_invert_pos[i] and not(show_invert_subpos) then show = 1
	if stop_flag_subpos=2 and stop_flag_pos=1 and show_invert_subpos then 
		show = 0
		show_mark = 0
	endif
endif

"end_show":
show=show
show_mark=show_mark
PARAMETERS show=show, show_mark=show_mark
end show, show_mark

"check_show_subpos":
!stop_flag_subpos = 0 - совпадений не найдено
!stop_flag_subpos = 1 - отображаются все сборки.
!stop_flag_subpos = 2 - найдено совпадение по сборке.
	stop_flag_subpos = 0
	usl1 = (show_subpos[i]=subpos) !По умолчанию - строгое равенство
	if (subpos="" or subpos=" ") and not(usl1) then !Для элементов вне сборок
		usl1 = 1
		stop_flag_subpos = 1
	endif
	if STRSUB(show_subpos[i], 1, 1) = "*" and STRLEN(show_subpos[i])=1 and not(usl1) then !Все сборки
		usl1 = 1
		stop_flag_subpos = 1
	endif
	if STRSUB(show_subpos[i], 1, 1) = "*" and not(usl1) then !Если сборка начинается с * - отсекаем, и ищем конец
		findsp = STRSUB(show_subpos[i], 2, STRLEN(show_subpos[i]))
		if STRLEN(subpos)>STRLEN(findsp) then !Если имя сборки меньше длины искомого текста - она явно не подходит
			findsp_t = STRSUB(subpos,STRLEN(subpos)-STRLEN(findsp)+1,STRLEN(findsp))
			usl1 = (findsp_t=findsp)
		else
			usl1 = 0
		endif
	endif
	if STRSUB(show_subpos[i], STRLEN(show_subpos[i]), 1) = "*" and not(usl1) then !Если сборка заканчивается на * - отсекаем, и ищем начало
		findsp = STRSUB(show_subpos[i], 1, STRLEN(show_subpos[i])-1)
		if STRLEN(subpos)>STRLEN(findsp) then !Если имя сборки меньше длины искомого текста - она явно не подходит
			findsp_t = STRSUB(subpos,1,STRLEN(findsp))
			usl1 = (findsp_t=findsp)
		else
			usl1 = 0
		endif
	endif
	if STRSUB(show_subpos[i], 1, 1) = "*" and STRSUB(show_subpos[i], STRLEN(show_subpos[i]), 1) = "*" and not(usl1)  then !Ищем вхождение
		findsp = STRSUB(show_subpos[i], 2, STRLEN(show_subpos[i])-2)
		usl1 = (STRSTR(subpos, findsp, 1)>0)
	endif
	if usl1 then stop_flag_subpos = 2
return

"check_show_pos":
!stop_flag_pos = 0 - совпадений не найдено
!stop_flag_pos = 1 - отображаются все поз.
!stop_flag_pos = 2 - найдено совпадение по поз.
	stop_flag_pos = 0
	if show_id[i]=0 and n_id[i]>EPS then
		for j=1 to vardim2(show_subpos_id)
			if vartype(show_subpos_id[i][j])=1 then show_subpos_id[i][j]="!!!!"
			usl2 = (show_subpos_id[i][j]=pos) !По умолчанию - строгое равенство
			if STRSUB(show_subpos_id[i][j], 1, 1) = "*" and STRLEN(show_subpos_id[i][j])=1 and not(usl2) then !Все ID
				usl2 = 1
				stop_flag_pos = 1
			endif
			if STRSUB(show_subpos_id[i][j], 1, 1) = "*" and not(usl2) then !Если ID начинается с * - отсекаем, и ищем конец
				findsp = STRSUB(show_subpos_id[i][j], 2, STRLEN(show_subpos[i]))
				if STRLEN(subpos)>STRLEN(findsp) then !Если имя сборки меньше длины искомого текста - она явно не подходит
					findsp_t = STRSUB(pos,STRLEN(pos)-STRLEN(findsp)+1,STRLEN(findsp))
					usl2 = (findsp_t=findsp)
				else
					usl2 = 0
				endif
			endif
			if STRSUB(show_subpos_id[i][j], STRLEN(show_subpos_id[i][j]), 1) = "*" and not(usl2) then !Если ID заканчивается на * - отсекаем, и ищем начало
				findsp = STRSUB(show_subpos_id[i][j], 1, STRLEN(show_subpos_id[i][j])-1)
				if STRLEN(pos)>STRLEN(findsp) then !Если ID меньше длины искомого текста - она явно не подходит
					findsp_t = STRSUB(pos,1,STRLEN(findsp))
					usl2 = (findsp_t=findsp)
				else
					usl2 = 0
				endif
			endif
			if STRSUB(show_subpos_id[i][j], 1, 1) = "*" and STRSUB(show_subpos_id[i][j], STRLEN(show_subpos_id[i][j]), 1) = "*" and not(usl2) then !Ищем вхождение
				findsp = STRSUB(show_subpos[i], 2, STRLEN(show_subpos[i])-2)
				usl2 = (STRSTR(pos, findsp, 1)>0)
			endif
			if usl2 then
				j=vardim2(show_subpos_id)
				stop_flag_pos = 2
			endif
		next j
	else
		stop_flag_pos = 1
	endif
return

"trim_space":
	if vartype(var) = 2 then
		DO
			is_trim = 0
			lvar = strlen(var)
			is_first_space = (strstr(STRSUB (var, 1, 1),space)>0)
			is_end_space = (strstr(STRSUB (var, lvar, 1),space)>0)
			if is_first_space then
				var = STRSUB (var, 2, lvar-1)
				lvar = strlen(var)
			endif
			if is_end_space then var = STRSUB (var, 1, lvar-1)
			is_trim = is_first_space + is_end_space
		WHILE is_trim <> 0
	endif
return
