NOT_SET = -1
NOT_SHOW = 0
SHOW_3D = 1
SHOW_TOP = 2
SHOW_SIDE = 3
show_type = NOT_SET

StandingB = 1
TemporaryB = 2
HstrengthB = 3
ChimanchorB = 4
ManchorB = 5
FoundB = 6

show_crew = 0 : success = LIBRARYGLOBAL ("Libraryflobals-km", "show_crew", show_crew)

if abs(success)<EPS then
	show_type = SHOW_3D
	show_wash_list = SHOW_3D
	show_hole = SHOW_3D
	goto "m_end"
endif

if show_crew=0 then
	show_type = NOT_SHOW
else
	bolt_show_all = 0
	success = LIBRARYGLOBAL ("Libraryflobals-km", "bolt_show_all", bolt_show_all)
	if bolt_show_all=1 then show_type = SHOW_3D
endif

if show_type = NOT_SET then
	inx = 0
	inxb=0
	dim bolt_ang1[15][2]
	dim bolt_ang2[15][2]
	bolt_hide_inv = 0
	success = LIBRARYGLOBAL ("Libraryflobals-km", "bolt_ang1", bolt_ang1)
	success = LIBRARYGLOBAL ("Libraryflobals-km", "bolt_ang2", bolt_ang2)
	success = LIBRARYGLOBAL ("Libraryflobals-km", "bolt_hide_inv", bolt_hide_inv)
	for i=1 to vardim1(bolt_ang1)
		usl1 = 0
		usl2 = 0
		flt = 0
		if bolt_ang1[i][1]<>bolt_ang1[i][2] then
			max_ang1 = max(bolt_ang1[i][1],bolt_ang1[i][2])
			min_ang1 = min(bolt_ang1[i][1],bolt_ang1[i][2])
			usl1 = ((ang1<max_ang1)*(ang1>min_ang1))
		else
			max_ang1=bolt_ang1[i][1]
			min_ang1=-1
			usl1 = (ang1=max_ang1)
			if usl1 then flt = flt + 1
		endif
		if bolt_ang2[i][1]<>bolt_ang2[i][2] then
			max_ang2 = max(bolt_ang2[i][1],bolt_ang2[i][2])
			min_ang2 = min(bolt_ang2[i][1],bolt_ang2[i][2])
			usl2 = ((ang2<max_ang2)*(ang2>min_ang2))
		else
			max_ang2=bolt_ang2[i][1]
			min_ang2=-1
			usl2 = (ang2=max_ang2)
			if usl2 then flt = flt + 1
		endif
		if usl1*usl2 then
			inx = i
			if flt=1 then inxb=i
			if flt=2 then i = vardim1(bolt_ang1)
		endif
	next i
	if inx>0 and inxb>0 then inx=inxb
	if inx = 0 then
		if bolt_hide_inv=0 then 
			show_type = SHOW_3D
		else
			show_type = NOT_SHOW
		endif
	else
		dim bolt_top[15]
		dim bolt_side[15]
		dim bolt_hide[15]
		success = LIBRARYGLOBAL ("Libraryflobals-km", "bolt_top", bolt_top)
		success = LIBRARYGLOBAL ("Libraryflobals-km", "bolt_side", bolt_side)
		success = LIBRARYGLOBAL ("Libraryflobals-km", "bolt_hide", bolt_hide)
		if bolt_hide[inx]=1 and bolt_hide_inv=0 then show_type = NOT_SHOW
		if bolt_hide[inx]=0 and bolt_hide_inv=1 then show_type = NOT_SHOW
		if bolt_side[inx]=1 then show_type = SHOW_SIDE
		if bolt_top[inx]=1 then show_type = SHOW_TOP
		if show_type = NOT_SET then show_type = SHOW_3D
	endif
endif

show_el=-1
call "Показ_по_группам" PARAMETERS ALL el_views=el_views RETURNED_PARAMETERS show_el
if show_el=0 then show_type=NOT_SHOW

show_wash_list = NOT_SET
show_hole = NOT_SET

success = LIBRARYGLOBAL ("Libraryflobals-km", "show_list", show_wash_list)
if show_wash_list=1 then show_wash_list=SHOW_3D
if show_type<>SHOW_3D and show_wash_list=SHOW_3D then show_wash_list=NOT_SHOW
success = LIBRARYGLOBAL ("Libraryflobals-km", "show_hole", show_hole)

"m_end":
parameters show_type=show_type, show_wash_list = show_wash_list, show_hole = show_hole
end show_type, show_wash_list, show_hole
