
!!-------------- Тип конструкции ------------------------
dim tip_konstr_m[]
success = LIBRARYGLOBAL ("Libraryflobals-km", "konstr_type_arr", tip_konstr_m)
if abs(success)>EPS then values "tip_konstr" tip_konstr_m, CUSTOM
!!------------------------------------------------------
if abs(ang2)<EPS then
	parameters ac_toplevel = diaB
	parameters ac_bottomlevel = 0
else
	parameters ac_toplevel = (sin(ang2)*lenB)
	parameters ac_bottomlevel = 0
endif
parameters A = diaB
parameters B = diaB
parameters ZZYZX = lenB
if kol_vo_spec<=0 then parameters kol_vo_spec=1
values{2} "type_hole" itype_hole, ttype_hole
values{2} "bolt_type" ibolt_type, tbolt_type
values{2} "is_stud" iis_stud, tis_stud
values{2} "qty_nut_end" iqty_nut, tqty_nut
values{2} "qty_nut_start" iqty_nut, tqty_nut
values{2} "qty_wash_end" iqty_wash, tqty_wash
values{2} "qty_wash_start" iqty_wash, tqty_wash
values "wash2_type" twash2_type
values "thread" "Complete", "Partial"
values "ang_hole" range [0,360]
if GLOB_MODPAR_NAME="coord_hole" then
	if type_hole=2 then
		if coord_hole[1]<0 then coord_hole[1]=0
		if coord_hole[2]>0 then coord_hole[2]=0
	endif
	if type_hole=3 then
		if coord_hole[1]<>0 then coord_hole[1]=0
		if coord_hole[2]>0 then coord_hole[2]=0
	endif
	parameters coord_hole=coord_hole
endif

if not((qty_wash_end=1) or (qty_wash_end=2) or (qty_wash_start=1) or (qty_wash_start=2)) then HIDEPARAMETER "wash1_dim"
if not((qty_wash_start=3) or (qty_wash_start=2) or (qty_wash_end=3) or (qty_wash_end=2)) then HIDEPARAMETER "wash2_dim"
if not(qty_nut_start or qty_nut_end) then HIDEPARAMETER "nut_dim"
if wash2_type="Прямоугольная" then
	HIDEPARAMETER "wash2_diam", "wash2_diam_in"
	values "wash2_a" range(diaB,)
	values "wash2_b" range(diaB,)
	values "wash2_thk" range(0,)
	parameters wash2_diam=0, wash2_diam_in=0
endif
if wash2_type="Круглая" then
	HIDEPARAMETER "wash2_a", "wash2_b"
	values "wash2_diam" range(diaB+0.001,)
	values "wash2_diam_in" range(diaB,)
	values "wash2_thk" range(0,)
	parameters wash2_a=0, wash2_b=0
endif

if abs(hit-lenB)<EPS then
	thread="Complete"
else
	thread="Partial"
endif
parameters thread=thread

if not(is_stud) then
	if bolt_type=TemporaryB or bolt_type=HstrengthB or bolt_type=StandingB then
		values "bolt_subtype" "ГОСТ 7798-70"
		values "marka" "3.6", "4.6", "4.8", "5.6", "5.8", "6.6", "6.8", "8.8", "9.8", "10.9", "12.9",CUSTOM
	endif
	if bolt_type=FoundB then
		values "bolt_subtype" "ГОСТ 24379.1-2012"
		values "isp" "с отгибом", "с анк. плитой", "прямой"
	endif
	if bolt_subtype="ГОСТ 7798-70" then gosub "7798"
	if bolt_subtype="ГОСТ 24379.1-2012" then gosub "24379"
else
	PARAMETERS bolt_subtype=""
	gosub "stud"
	parameters hit=lenB
endif

if vardim1(el_views)<=1 then
	dim el_tmp[]
	for i=1 to 20
		el_tmp[i] = " "
	next i
	parameters el_views = el_tmp
endif

diam_hole = str("%.0mm", bolt_body+holeTolerance*2)
dist_hole = str("%.0mm", coord_hole[1] + abs(coord_hole[2]))
if type_hole = 1 then hole_diam = diam_hole
if type_hole = 2 then hole_diam = diam_hole + "x" + dist_hole
if type_hole = 3 then hole_diam = diam_hole + "x" + dist_hole
hole_diam = "отв. d" + hole_diam 
parameters hole_diam = hole_diam

end

"stud":
	file_bolt = "Болты 7798-70.txt"
	file_nut = "Гайки 5915-70.txt"
	file_wash = "Шайбы 11371-78.txt"
	PARAMETERS nut_type = file_nut
	PARAMETERS wash1_type = file_wash
	gosub "get_diam_bolt" : diaB_arr = d_arr
	values "diaB" diaB_arr
	if GLOB_MODPAR_NAME="diaB" then
		diam = diaB
		gosub "get_dim"
	endif
return

"24379":
	file_bolt = "Болты 24379.1-2012.txt"
	file_nut = "Гайки 5915-70.txt"
	file_wash = "Шайбы 24379.1-2012.txt"
	lock "hit", "marka", "thread"
	PARAMETERS nut_type = file_nut
	PARAMETERS wash1_type = file_wash
	HIDEPARAMETER "bolt_wrench"
	gosub "get_diam_bolt" : diaB_arr = d_arr
	values "diaB" diaB_arr
	if GLOB_MODPAR_NAME="diaB" then
		diam = diaB
		gosub "get_dim_nut"
		parameters nut_wrench=nut_wrench_t, nut_thk=nut_thk_t, nut_diam=nut_diam_t
		gosub "get_dim_wash"
		parameters wash1_thk=wash1_thk_t, wash1_diam=wash1_diam_t, wash1_diam_in=wash1_diam_in_t

		ch2 = open("text", file_bolt, "separator='\t', mode=ro, LIBRARY")
		t = 0 : n = input (ch2, 2, inxd, t) : bolt_tpi_t1 = t/1000
		t = 0 : n = input (ch2, 4, inxd, t) : bolt_body_t = t/1000
		t = 0 : n = input (ch2, 8, inxd, t) : bolt_hit_t = t/1000
		t = 0 : n = input (ch2, 9, inxd, t) : bolt_lz_t = t/1000
		t = 0 : n = input (ch2, 10, inxd, t) : bolt_rad_t = t/1000
		t = 0 : n = input (ch2, 11, inxd, t) : bolt_ank_t = t/1000
		close ch2
		parameters bolt_body=bolt_body_t, bolt_wrench="", bolt_thk=bolt_lz_t, bolt_diam=bolt_ank_t, bolt_tpi=bolt_tpi_t1, hit=bolt_hit_t
	endif
	PARAMETERS obozn = "ГОСТ 24379.1-2012 "
	PARAMETERS ves = lenB*7850*3.14*diaB*diaB/4
	if isp = "с отгибом" then isp_t = " 1.1 "
	if isp = "с анк. плитой" then isp_t = " 2.1 "
	if isp = "прямой" then isp_t = " 5 "
	PARAMETERS naen = "Болт" + isp_t + "М" + str("%.0mm",diaB) + "x" + str("%.0mm",lenB)
return

"7798":
	file_bolt = "Болты 7798-70.txt"
	file_nut = "Гайки 5915-70.txt"
	file_wash = "Шайбы 11371-78.txt"
	PARAMETERS nut_type = file_nut
	PARAMETERS wash1_type = file_wash
	HIDEPARAMETER "qty_nut_start"
	PARAMETERS qty_nut_start = 0
	lock "hit", "thread"
	gosub "get_diam_bolt" : diaB_arr = d_arr
	values "diaB" diaB_arr
	if GLOB_MODPAR_NAME="diaB" then
		diam = diaB : gosub "get_bolt_len"
		parameters len_arr = len_arr_t, hit_arr = hit_arr_t
		gosub "get_dim"
	endif
	values "lenB" len_arr

	if GLOB_MODPAR_NAME="diaB" or GLOB_MODPAR_NAME="lenB" then
		diam = diaB : len = lenB : gosub "get_bolt_hit"
		parameters hit = hit_t
	endif
	parameters obozn = "ГОСТ 7798-70"
	parameters naen = "Болт "+"M"+str("%.0",diaB*1000)
return

"get_dim":
	gosub "get_dim_bolt"
	values "bolt_tpi" bolt_tpi_t1, bolt_tpi_t2
	parameters bolt_body=bolt_body_t, bolt_wrench=bolt_wrench_t, bolt_thk=bolt_thk_t, bolt_diam=bolt_diam_t

	gosub "get_dim_nut"
	parameters nut_wrench=nut_wrench_t, nut_thk=nut_thk_t, nut_diam=nut_diam_t

	gosub "get_dim_wash"
	parameters wash1_thk=wash1_thk_t, wash1_diam=wash1_diam_t, wash1_diam_in=wash1_diam_in_t
return

"get_dim_nut":
	file_name = file_nut : gosub "get_inx"
	ch2 = open("text", file_name, "separator='\t', mode=ro, LIBRARY")
	t = 0 : n = input (ch2, 2, inxd, t) : nut_wrench_t = str("%.0",t)
	t = 0 : n = input (ch2, 3, inxd, t) : nut_thk_t = t/1000
	t = 0 : n = input (ch2, 4, inxd, t) : nut_diam_t = t/1000
	close ch2
return

"get_dim_bolt":
	file_name = file_bolt : gosub "get_inx"
	ch2 = open("text", file_name, "separator='\t', mode=ro, LIBRARY")
	t = 0 : n = input (ch2, 2, inxd, t) : bolt_tpi_t1 = t/1000
	t = 0 : n = input (ch2, 3, inxd, t) : bolt_tpi_t2 = t/1000
	t = 0 : n = input (ch2, 4, inxd, t) : bolt_body_t = t/1000
	t = 0 : n = input (ch2, 5, inxd, t) : bolt_wrench_t = str("%.0",t)
	t = 0 : n = input (ch2, 6, inxd, t) : bolt_thk_t = t/1000
	t = 0 : n = input (ch2, 7, inxd, t) : bolt_diam_t = t/1000
	close ch2
return

"get_dim_wash":
	file_name = file_wash : gosub "get_inx"
	ch2 = open("text", file_name, "separator='\t', mode=ro, LIBRARY")
	t = 0 : n = input (ch2, 2, inxd, t) : wash1_diam_in_t = t/1000
	t = 0 : n = input (ch2, 4, inxd, t) : wash1_diam_t = t/1000
	t = 0 : n = input (ch2, 5, inxd, t) : wash1_thk_t = t/1000
	close ch2
return

"get_diam_bolt":
	ch2 = open("text", file_bolt, "separator='\t', mode=ro, LIBRARY")
	n = 1 : j = 1 : k = 2 : t = 0
	while  n<>-1 and k<=40 do
		n = input (ch2, 1, k, t)
		if n <> -1 then
			if vartype(t)=1 then
				if t>0 then
					d_arr[j]=t/1000
					j = j + 1
					t = 0
				endif
			endif
		endif
		k = k + 1
	endwhile
	close ch2
return

"get_bolt_len":
	file_name = file_bolt : gosub "get_inx"
	ch1 = open("text", file_bolt, "separator='\t', mode=ro, LIBRARY")
	i = 8 : j = 1 : lhit = 0 : l = 0
	n1 = 1 : n2 = 1
	while n1 <> -1 and n2 <> -1 and i<1000 do
		n1 = input (ch1, i, inxd, lhit)
		n2 = input (ch1, i, 1, l)
		if vartype(lhit)=1 and vartype(l)=1 then
			if lhit>=0 and l>0 then
				if lhit=0 then lhit=l
				len_arr_t[j] = l/1000
				hit_arr_t[j] = lhit/1000
				j = j + 1
			endif
		endif
		i = i + 1
	endwhile
	close ch1
return

"get_bolt_hit":
	gosub "get_bolt_len"
	inxl=1
	for i=1 to vardim1(len_arr_t)
		if len=len_arr_t[i] then
			inxl=i
			i=vardim1(len_arr_t)
		endif
	next i
	hit_t = hit_arr_t[inxl]
return

"get_inx":
	ch2 = open("text", file_name, "separator='\t', mode=ro, LIBRARY")
	inxd=1
	n = 1 : j = 1 : k = 2 : t = 0
	while  n<>-1 and k<=40 do
		n = input (ch2, 1, k, t)
		if n <> -1 then
			if vartype(t)=1 then
				if abs(t-diam*1000)<EPS then
					inxd=k
					n = -1
					k = 41
				endif
			endif
		endif
		k = k + 1
	endwhile
	close ch2
return