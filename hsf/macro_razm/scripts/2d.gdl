if abs(razm_l)<EPS then end
PAPER_TO_MODEL = GLOB_SCALE / 1000
if mirrored<EPS and abs(mirrored)>EPS then mirrored = SYMB_MIRRORED
if rotang<EPS and abs(rotang)>EPS then rotang = SYMB_ROTANGLE
if GLOB_VIEW_TYPE = 8 or GLOB_VIEW_TYPE = 9 or GLOB_PREVIEW_MODE = 2 then
	mirrored = 0
	rotang = 0
endif
ang_symb = (ang_razm+90) MOD 360
!Отдаление текста, после которого начинается построение выноски
accuare = 4*PAPER_TO_MODEL
r = request ("View_Rotangle", "", _angleViewRot)
sgnx = SGN(coord_razm[inx_razm][5])
sgnd = 1
if abs(dx)>EPS then 
	sgnd = SGN(dx)
else
	if abs(dy)>EPS then sgnd = SGN(dy)
endif

ang_tot = -ang_symb * (mirrored - not(mirrored))
ang_tot = (360+ang_tot+ _angleViewRot+rotang) MOD 360

angle_txt = -90
if mirrored then angle_txt = angle_txt - 180
if (ang_tot > (180 + EPS) & ang_tot < (360 + EPS)) then angle_txt = angle_txt - 180
if abs(ang_tot)<EPS then angle_txt = angle_txt - 180

!Поворот засечек
angle_zas = 45 * (mirrored - not(mirrored))+180
if ((_angleViewRot+rotang) > (90 + EPS) & (_angleViewRot+rotang) < (270 + EPS)) then angle_zas = angle_zas - 180
!Размерные стили
width_factor = 1
charspace_factor = 1
razm_anc = 5
DEFINE STYLE{2} "razm" Shrift , Shrift_razm, Shrift_tip
sss=request("Height_of_style", "razm", height, descent, leading)
dx_txt = (height/2-descent-leading-Shrift_razm/2)*PAPER_TO_MODEL*0.5
height = height * PAPER_TO_MODEL

!Редектирование начала/конца
if use_hotspot then
	HOTSPOT2 0, coord_razm[inx_razm][2], unID, coord_razm[inx_razm][1], 1+128, coord_razm[inx_razm][1], hotspotname[1] : unID=unID+1
	HOTSPOT2 coord_razm[inx_razm][1], coord_razm[inx_razm][2], unID, coord_razm[inx_razm][1], 2, coord_razm[inx_razm][1], hotspotname[1]: unID=unID+1
	HOTSPOT2 -1, coord_razm[inx_razm][2], unID, coord_razm[inx_razm][1], 3, coord_razm[inx_razm][1], hotspotname[1]: unID=unID+1

	HOTSPOT2 coord_razm[inx_razm][1], 0, unID, coord_razm[inx_razm][2], 1+128, coord_razm[inx_razm][2], hotspotname[2]: unID=unID+1
	HOTSPOT2 coord_razm[inx_razm][1], coord_razm[inx_razm][2], unID, coord_razm[inx_razm][2], 2, coord_razm[inx_razm][2], hotspotname[2]: unID=unID+1
	HOTSPOT2 coord_razm[inx_razm][1], -1, unID, coord_razm[inx_razm][2], 3, coord_razm[inx_razm][2], hotspotname[2]: unID=unID+1

	HOTSPOT2 0, coord_razm[inx_razm][4], unID, coord_razm[inx_razm][3], 1+128, coord_razm[inx_razm][3], hotspotname[3]: unID=unID+1
	HOTSPOT2 coord_razm[inx_razm][3], coord_razm[inx_razm][4], unID, coord_razm[inx_razm][3], 2, coord_razm[inx_razm][3], hotspotname[3]: unID=unID+1
	HOTSPOT2 -1, coord_razm[inx_razm][4], unID, coord_razm[inx_razm][3], 3, coord_razm[inx_razm][3], hotspotname[3]: unID=unID+1

	HOTSPOT2 coord_razm[inx_razm][3], 0, unID, coord_razm[inx_razm][4], 1+128, coord_razm[inx_razm][4], hotspotname[4]: unID=unID+1
	HOTSPOT2 coord_razm[inx_razm][3], coord_razm[inx_razm][4], unID, coord_razm[inx_razm][4], 2, coord_razm[inx_razm][4], hotspotname[4]: unID=unID+1
	HOTSPOT2 coord_razm[inx_razm][3], -1, unID, coord_razm[inx_razm][4], 3, coord_razm[inx_razm][4], hotspotname[4]: unID=unID+1
endif
if dbg then
	text2 coord_razm[inx_razm][1], coord_razm[inx_razm][2],"1"
	text2 coord_razm[inx_razm][3], coord_razm[inx_razm][4],"2"
endif
!Стали в начало, развернулись
add2 coord_razm[inx_razm][1], coord_razm[inx_razm][2]
rot2 ang_symb

!Редактирование отступа
if use_hotspot then
	HOTSPOT2 0, razm_l/3, unID, coord_razm[inx_razm][5], 1+128, coord_razm[inx_razm][5], hotspotname[5]: unID=unID+1
	HOTSPOT2 coord_razm[inx_razm][5], razm_l/3, unID, coord_razm[inx_razm][5], 2, coord_razm[inx_razm][5], hotspotname[5]: unID=unID+1
	HOTSPOT2 -1, razm_l/3, unID, coord_razm[inx_razm][5], 3, coord_razm[inx_razm][5], hotspotname[5]: unID=unID+1

	HOTSPOT2 0, razm_l/4, unID, coord_razm[inx_razm][10], 1+128, coord_razm[inx_razm][10], hotspotname[10]: unID=unID+1
	HOTSPOT2 coord_razm[inx_razm][10]*sgnx, razm_l/4, unID, coord_razm[inx_razm][10], 2, coord_razm[inx_razm][10], hotspotname[10]: unID=unID+1
	HOTSPOT2 -1*sgnx, razm_l/4, unID, coord_razm[inx_razm][10], 3, coord_razm[inx_razm][10], hotspotname[10]: unID=unID+1
endif

if use_hotspot then
	otstup = coord_razm[inx_razm][5]
else
	otstup = coord_razm[inx_razm][5] * PAPER_TO_MODEL
endif

otstup_vinosnih = coord_razm[inx_razm][10]
if abs(zasechka) < EPS then zasechka = 1 * PAPER_TO_MODEL
if abs(vistup) < EPS then vistup = zasechka
if abs(otstup) < EPS then otstup = max(5 * PAPER_TO_MODEL, height+dx_txt)
sgnx = SGN(otstup)
if abs(otstup) < vistup then otstup = vistup*sgnx
if otstup_vinosnih < EPS then otstup_vinosnih = vistup*0.2
if abs(otstup)-otstup_vinosnih<vistup then otstup_vinosnih = abs(otstup)-vistup

!Линия размера
line2 otstup, -vistup, otstup, razm_l + vistup
!Выносная к первой точке
line2 otstup_vinosnih*sgnx, 0, otstup + vistup*sgnx, 0
!Выносная ко второй точке
line2 otstup_vinosnih*sgnx, razm_l, otstup + vistup*sgnx, razm_l

!Засечки
add2 otstup, 0
rot2 angle_zas
line2 -zasechka, 0, zasechka, 0
del 2
add2 otstup, razm_l
rot2 angle_zas
line2 -zasechka, 0, zasechka, 0
del 2

!Подготовка текстовых блоков и определение необходимости сдвинуть текст
txtv = fstr_razm : tblockname = "txt_razm_f" : name_style = "razm" : gosub "get_block"
razm_f_l = 0.5*tbwidth
if STRLEN(sstr_razm)>EPS then
	txtv = sstr_razm : tblockname = "txt_razm_s" : name_style = "razm" : gosub "get_block"
	razm_s_l = 0.5*tbwidth
	razm_fs = max(razm_f_l, razm_s_l)
else
	razm_fs = razm_f_l
	razm_s_l = 0
endif

!Сдвигаем первый размер, если нужно, и если положение текста не задано (равно 0)
!Сдвигаем всегда вправо
if (razm_fs-razm_l*0.5)>EPS and not(abs(coord_razm[inx_razm][6])>EPS or abs(coord_razm[inx_razm][7])>EPS) and not(abs(coord_razm[inx_razm][8])>EPS or abs(coord_razm[inx_razm][9])>EPS) then
	if not(side_otstup_razm) then
		coord_razm[inx_razm][7] = razm_fs+razm_l/2+zasechka
		coord_razm[inx_razm][9] = razm_fs+razm_l/2+zasechka
	else
		coord_razm[inx_razm][7] = -razm_fs-razm_l/2-zasechka
		coord_razm[inx_razm][9] = -razm_fs-razm_l/2-zasechka
	endif
endif

add2 otstup,razm_l/2
!!Первый размер
if use_hotspot then
	HOTSPOT2 0, coord_razm[inx_razm][7], unID, coord_razm[inx_razm][6], 1+128, coord_razm[inx_razm][6], hotspotname[6]: unID=unID+1
	HOTSPOT2 coord_razm[inx_razm][6], coord_razm[inx_razm][7], unID, coord_razm[inx_razm][6], 2, coord_razm[inx_razm][6], hotspotname[6]: unID=unID+1
	HOTSPOT2 -1, coord_razm[inx_razm][7], unID, coord_razm[inx_razm][6], 3, coord_razm[inx_razm][6], hotspotname[6]: unID=unID+1

	HOTSPOT2 coord_razm[inx_razm][6], 0, unID, coord_razm[inx_razm][7], 1+128, coord_razm[inx_razm][7], hotspotname[7]: unID=unID+1
	HOTSPOT2 coord_razm[inx_razm][6], coord_razm[inx_razm][7], unID, coord_razm[inx_razm][7], 2, coord_razm[inx_razm][7], hotspotname[7]: unID=unID+1
	HOTSPOT2 coord_razm[inx_razm][6], -1, unID, coord_razm[inx_razm][7], 3, coord_razm[inx_razm][7], hotspotname[7]: unID=unID+1
endif
n_transform = 0
add2 coord_razm[inx_razm][6], coord_razm[inx_razm][7]
rot2 angle_txt
RICHTEXT2 0, dx_txt+tbheight/2, "txt_razm_f"
del 2
!Выносная линия
if abs(coord_razm[inx_razm][6])>accuare or abs(coord_razm[inx_razm][7])-razm_l/2>EPS then
	line2 coord_razm[inx_razm][6], coord_razm[inx_razm][7]-razm_f_l, coord_razm[inx_razm][6], coord_razm[inx_razm][7]+razm_f_l
	line2 coord_razm[inx_razm][6], coord_razm[inx_razm][7]-razm_f_l*sgn(coord_razm[inx_razm][7]), 0, 0
endif

if STRLEN(sstr_razm)>EPS then
	!Второй размер
	if use_hotspot then
		HOTSPOT2 0, coord_razm[inx_razm][9], unID, coord_razm[inx_razm][8], 1+128, coord_razm[inx_razm][8], hotspotname[8]: unID=unID+1
		HOTSPOT2 coord_razm[inx_razm][8], coord_razm[inx_razm][9], unID, coord_razm[inx_razm][8], 2, coord_razm[inx_razm][8], hotspotname[8]: unID=unID+1
		HOTSPOT2 -1, coord_razm[inx_razm][9], unID, coord_razm[inx_razm][8], 3, coord_razm[inx_razm][8], hotspotname[8]: unID=unID+1

		HOTSPOT2 coord_razm[inx_razm][8], 0, unID, coord_razm[inx_razm][9], 1+128, coord_razm[inx_razm][9], hotspotname[9]: unID=unID+1
		HOTSPOT2 coord_razm[inx_razm][8], coord_razm[inx_razm][9], unID, coord_razm[inx_razm][9], 2, coord_razm[inx_razm][9], hotspotname[9]: unID=unID+1
		HOTSPOT2 coord_razm[inx_razm][8], -1, unID, coord_razm[inx_razm][9], 3, coord_razm[inx_razm][9], hotspotname[9]: unID=unID+1
	endif
	add2 coord_razm[inx_razm][6], coord_razm[inx_razm][7]
	rot2 angle_txt
	RICHTEXT2 0, dx_txt-tbheight/2, "txt_razm_s"
	del 2
	!Выносная линия
	if abs(coord_razm[inx_razm][8])>accuare or abs(coord_razm[inx_razm][9])-razm_l/2>EPS then
		line2 coord_razm[inx_razm][8], coord_razm[inx_razm][9]-razm_s_l, coord_razm[inx_razm][8], coord_razm[inx_razm][9]+razm_s_l
		line2 coord_razm[inx_razm][8], coord_razm[inx_razm][9]-razm_s_l*sgn(coord_razm[inx_razm][9]), 0, 0
	endif
endif

del 3
end

"get_block":
	if vartype(tblockname)<>2 then tblockname = "emp"
	if vartype(txtv)<>2 then txtv = str("%.0",txtv)
	paragraph tblockname+"p" 2,
			0, 0, 0,
			1
			style name_style
			txtv
	endparagraph
	TEXTBLOCK tblockname 0, razm_anc, 0, width_factor, charspace_factor, 1,tblockname+"p"
	width = Shrift_razm : height = Shrift_razm
	n = REQUEST("TEXTBLOCK_INFO", tblockname, width, height)
	tbwidth = width * PAPER_TO_MODEL
	tbheight = height * PAPER_TO_MODEL
	sss=request("Height_of_style", name_style, height, descent, leading)
	dx_txt = (height/2-descent-leading-Shrift_razm/2)*PAPER_TO_MODEL
return
